# Default recipe: convert all Orgmode files
default: compile-all

# compile a single Orgmode file to Typst, PDF, and HTML
compile FILE:
    just compile-typst "{{FILE}}"
    just compile-pdf "{{FILE}}"
    just compile-html "{{FILE}}"

# compile Orgmode file to Typst
compile-typst FILE:
    #!/usr/bin/env fish
    if not string match -q "*.org" "{{FILE}}"
        echo "Error: File must have .org extension"
        exit 1
    end
    mkdir -p build/typst
    cp style.csl build/typst/
    cp references.bib build/typst/
    set BASENAME (basename "{{FILE}}" .org)
    pandoc --bibliography="./references.bib" --citeproc --csl ./style.csl -t typst -o "build/typst/$BASENAME.typ" "{{FILE}}"

# compile Typst file to PDF
compile-pdf FILE:
    #!/usr/bin/env fish
    mkdir -p build/pdf
    set BASENAME (basename "{{FILE}}" .org)
    typst compile "build/typst/$BASENAME.typ" "build/pdf/$BASENAME.pdf"

# compile Typst file to HTML
compile-html FILE:
    #!/usr/bin/env fish
    mkdir -p build/html
    set BASENAME (basename "{{FILE}}" .org)
    typst compile --features html "build/typst/$BASENAME.typ" "build/html/$BASENAME.html"

# compile all Orgmode files
compile-all:
    #!/usr/bin/env fish
    for file in *.org
        if test -f "$file"
            just compile "$file"
        end
    end

# Update typst flake input to latest version
update:
    nix flake update
